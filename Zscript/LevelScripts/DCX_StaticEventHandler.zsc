class DCX_StaticHandler : WadFusionStaticHandler Replaces WadFusionStaticHandler
{
	override void NetworkProcess(ConsoleEvent e)
	{
		if (e.Name ~== "NewGameChangeLevelInputEvent")
			DCX_NewGameChangeLevelInput(); // wf_newgame.zs
		
		if (e.Name ~== "IntermissionStoryEvent")
			Level.StartIntermission(intermission, FSTATE_INLEVELNOWIPE);
	}
	
	override void WorldTick()
	{
		if ( CVar.FindCVar("wf_compat_nextmap").GetBool() )
			FullRunStory(); // wf_fullrun.zs in wadfusion.pk3

		DCX_NewGameIntro();
		DCX_IntermissionStory(); // wf_story.zs in wadfusion.pk3
		
		FullRunMultiplayerTakeStuff(); // wf_story.zs in wadfusion.pk3
	}
	
	override void RenderOverlay(RenderEvent e)
	{
		DCX_NewGameTitlePic();
		DCX_FullRunMultiplayer();
	}
	
	ui void DCX_NewGameTitlePic()
	{
		string mapName = Level.MapName.MakeLower();
		if ( mapName.Left(10) == "wf_newgame" || mapName.Left(8) == "wf_story" )
		{
			Level.SetFrozen(true);
			
			TextureId titlePic = TexMan.CheckForTexture("TITLEPIC");
			TextureId titleD1 = TexMan.CheckForTexture("TITLED1");
			TextureId titleUD = TexMan.CheckForTexture("TITLEUD");
			TextureId titleS1 = TexMan.CheckForTexture("TITLES1");
			TextureId titleS2 = TexMan.CheckForTexture("TITLES2");
			TextureId titleD2 = TexMan.CheckForTexture("TITLED2");
			TextureId titleML = TexMan.CheckForTexture("TITLEML");
			TextureId titleNV = TexMan.CheckForTexture("TITLENV");
			TextureId titleLR = TexMan.CheckForTexture("TITLELR");
			TextureId titleTN = TexMan.CheckForTexture("TITLETN");
			TextureId titlePL = TexMan.CheckForTexture("TITLEPL");
			TextureId titleAB = TexMan.CheckForTexture("TITLEAB");
			
			// draw a black overlay over the entire screen
			// hides the HUD under the titlepic overlay that gets drawn next
			int resX = Screen.GetWidth();
			int resY = Screen.GetHeight();
			Screen.DrawTexture(titlePic, false, 0, 0, DTA_ScaleX, resX, DTA_ScaleY, resY, DTA_FillColor, 0);
			
			if ( mapName.Left(10) == "wf_newgame" )
			{
				if ( CVar.FindCVar("wf_compat_titlepics").GetBool() )
				{
					if ( nextMap == "e1m1" || nextMap == "e2m1" || nextMap == "e3m1" )
						Screen.DrawTexture(titleD1, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "e4m1" )
						Screen.DrawTexture(titleUD, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "e5m1" )
						Screen.DrawTexture(titleS1, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "e6m1" )
						Screen.DrawTexture(titleS2, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "map01" || nextMap == "map12" || nextMap == "map21")
						Screen.DrawTexture(titleD2, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "ml_map01" )
						Screen.DrawTexture(titleML, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "nv_map01" )
						Screen.DrawTexture(titleNV, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "lr_map01" || nextMap == "lr_map08" )
						Screen.DrawTexture(titleLR, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "tn_map01" || nextMap == "tn_map12" || nextMap == "tn_map21" )
						Screen.DrawTexture(titleTN, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "pl_map01" || nextMap == "pl_map12" || nextMap == "pl_map21" )
						Screen.DrawTexture(titlePL, false, 0, 0, DTA_FullScreen, 1);
					else if ( nextMap == "ab_map01" || nextMap == "ab_map12" || nextMap == "ab_map21" )
						Screen.DrawTexture(titleAB, false, 0, 0, DTA_FullScreen, 1);
					else
						Screen.DrawTexture(titlePic, false, 0, 0, DTA_FullScreen, 1);
				}
				else
					Screen.DrawTexture(titlePic, false, 0, 0, DTA_FullScreen, 1);
			}
			else
				Screen.DrawTexture(titlePic, false, 0, 0, DTA_FullScreen, 1);
		}
	}
	
	void DCX_NewGameIntro()
	{
		string mapName = Level.MapName.MakeLower();
		if ( mapName.Left(10) == "wf_newgame" )
		{
			if ( Level.MapTime == 0 )
			{
				// starting an empty intermission on the first tic removes
				// the screen wipe that usually happens when changing levels
				
				if ( !fullRunNewGame )
					Level.StartIntermission("WadFusionNewGame", FSTATE_INLEVELNOWIPE);
				
				if ( mapName != "wf_newgame" )
					nextMap = mapName.Mid(11);
				
				if ( CVar.FindCVar("wf_compat_titlepics").GetBool() )
				{
					// play title music for each game
					if ( nextMap == "e1m1" || nextMap == "e2m1" || nextMap == "e3m1" )
						S_ChangeMusic("d_intro", 0, false);
					else if ( nextMap == "e4m1" )
						S_ChangeMusic("d_intr2", 0, false);
					else if ( nextMap == "e5m1" )
						PlaySigilIntroMusic();
					else if ( nextMap == "e6m1" )
						PlaySigil2IntroMusic();
					else if ( nextMap == "map01" || nextMap == "map12" || nextMap == "map21" )
						S_ChangeMusic("d_dm2ttl", 0, false);
					else if ( nextMap == "ml_map01" )
						S_ChangeMusic("D_TITLEM", 0, false);
					else if ( nextMap == "nv_map01" )
						S_ChangeMusic("D_NRFTLT", 0, false);
					else if ( nextMap == "lr_map01" || nextMap == "lr_map08" )
						S_ChangeMusic("x_dm2ttl", 0, false);
					else if ( nextMap == "tn_map01" || nextMap == "tn_map12" || nextMap == "tn_map21" )
						S_ChangeMusic("t_dm2ttl", 0, false);
					else if ( nextMap == "pl_map01" || nextMap == "pl_map12" || nextMap == "pl_map21" )
						S_ChangeMusic("p_dm2ttl", 0, false);
					else if ( nextMap == "ab_map01" || nextMap == "ab_map12" || nextMap == "ab_map21" )
						S_ChangeMusic("D_TITLEA", 0, false);
					else
						NewGameChangeLevel();
				}
				else
					NewGameChangeLevel();
			}
		}
	}
	
	void DCX_NewGameChangeLevelInput()
	{
		if ( CVar.FindCVar("wf_intros").GetBool() )
		{
			if ( nextMap != "e2m1" && 
				 nextMap != "e3m1" && 
				 nextMap != "e4m1" && 
				 nextMap != "map12" &&
				 nextMap != "map21" &&
				 nextMap != "ml_map01" && 
				 nextMap != "lr_map08" &&
				 nextMap != "tn_map12" &&
				 nextMap != "tn_map21" &&
				 nextMap != "pl_map12" &&
				 nextMap != "pl_map21" &&
				 nextMap != "ab_map12" &&
				 nextMap != "ab_map21" )
				Level.ChangeLevel("wf_story", 0, CHANGELEVEL_RESETINVENTORY|CHANGELEVEL_RESETHEALTH|CHANGELEVEL_NOINTERMISSION);
			else
				Level.ChangeLevel(nextMap, 0, CHANGELEVEL_RESETINVENTORY|CHANGELEVEL_RESETHEALTH|CHANGELEVEL_NOINTERMISSION);
		}
		else
			Level.ChangeLevel(nextMap, 0, CHANGELEVEL_RESETINVENTORY|CHANGELEVEL_RESETHEALTH|CHANGELEVEL_NOINTERMISSION);
	}
	
	ui void DCX_FullRunMultiplayer()
	{
		string mapName = Level.MapName.MakeLower();
		
		if ( mapName == "wf_story" )
		{
			if ( fullRunFinished && multiplayer )
				Screen.DrawText(smallfont, Font.CR_UNTRANSLATED, 1, 1, "$WF_FULLRUN_END_MULTIPLAYER", DTA_320x200, true);
		}
	}
	
	void DCX_IntermissionStory()
	{
		string mapName = Level.MapName.MakeLower();
		
		if ( mapName.Left(8) == "wf_story" )
		{
			if ( mapName.Mid(9, 6) != "ml_map" )
			{
				// play episode into stories
				if ( nextMap == "e1m1" )
					intermission = "Doom1_Intro";
				else if ( nextMap == "e5m1" )
					intermission = "Sigil_Intro";
				else if ( nextMap == "e6m1" )
					intermission = "Sigil2_Intro";
				else if ( nextMap == "map01" )
					intermission = "Doom2_Intro";
				else if ( nextMap == "nv_map01" )
					intermission = "Nerve_Intro";
				else if ( nextMap == "lr_map01" )
					intermission = "Id1_Intro";
				else if ( nextMap == "tn_map01" )
					intermission = "Tnt_Intro";
				else if ( nextMap == "pl_map01" )
					intermission = "Plutonia_Intro";
				else if ( nextMap == "ab_map01" )
					intermission = "Absolution_Intro";
				
				StoryStartIntermission();
				
				// don't skip the screen wipe when loading a newgame hack map
				int fullRun = CVar.FindCVar("wf_fullrun").GetInt();
				if ( fullRun >= 1 && fullRun <= 3 )
					fullRunNewGame = true;
				
				// change to the map which was set in NewGameIntro() or FullRun()
				if ( Level.MapTime >= 1 )
				{
					if ( !fullRunFinished )
						Level.ChangeLevel(nextMap, 0, CHANGELEVEL_RESETINVENTORY|CHANGELEVEL_RESETHEALTH|CHANGELEVEL_NOINTERMISSION);
					// try changing to a level that doesn't exist
					// this triggets the default ending sequence -- Fusion_GotoTitle
					else if ( !multiplayer )
						Level.ChangeLevel("", 0, CHANGELEVEL_NOINTERMISSION);
				}
			}
			else
			{
				// hack for adding optional story intermissions in the master levels rejects
				string mapSuffix = mapName.Mid(15, 2);
				intermission = "MasterLevels_Map"..mapSuffix;
				nextMap = "ml_map"..mapSuffix;
				
				StoryStartIntermission();
				
				if ( Level.MapTime >= 1 )
				{
					if ( mapSuffix == "29" || mapSuffix == "30" ||
						 mapSuffix == "31" || mapSuffix == "32" ||
						 mapSuffix == "16" || mapSuffix == "17" ||
						 mapSuffix == "33" || mapSuffix == "19" )
					{
						Level.ChangeLevel(nextMap, 0, CHANGELEVEL_RESETINVENTORY|CHANGELEVEL_RESETHEALTH|CHANGELEVEL_NOINTERMISSION);
					}
					else
						Level.ChangeLevel(nextMap, 0, CHANGELEVEL_NOINTERMISSION);
				}
			}
		}
	}
}